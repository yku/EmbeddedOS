/* OUTPUT_FORMAT("elf32-h8300") */
OUTPUT_ARCH(h8300h)
ENTRY("_start")

MEMORY
{
	romall(rx)	: o = 0x000000, l = 0x080000
	vectors(r)	: o = 0x000000, l = 0x000100
	rom(rx)         : o = 0x000100, l = 0x07ff00

	ramall(rx)	: o = 0xffbf20, l = 0x004000
	data(rwx)	: o = 0xfffc20, l = 0x000300
	stack(rx)	: o = 0xffff00, l = 0x000000
}

SECTIONS
{

	.vectors : {
		vector.o(.data)
	} > vectors

	.text : {
		_text_start = . ;
		*(.text)
		_etext = . ;
	} > rom

	.rodata : {
		_rodata_start = . ;
		*(.strings)
		*(.rodata)
		*(.rodata.*)
		_erodata = .;
	} > rom

	.data : {
		_data_start = . ;
		*(.data)
		_edata = . ;
	} > data AT> rom

	.bss : {
		_bss_start = . ;
		*(.bss)
		*(COMMON)
		_ebss = . ;
	} > data AT> rom

	. = ALIGN(4);
	_end = . ;
	
	.stack : {
		_stack = .;
	} > stack
}

